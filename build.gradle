plugins {
    id 'jacoco'
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'org.jetbrains.kotlin.kapt' version '1.6.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.6.21'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.6.21'
    id 'org.springframework.boot' version '2.7.16'
}

apply plugin: "io.spring.dependency-management"

version='sprint'
group = 'io.openfuture.chain'
sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    // Kotlin
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.jetbrains.kotlin:kotlin-reflect")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')

    // Spring
    implementation('org.springframework.boot:spring-boot-starter-webflux')
    implementation("org.springframework.boot:spring-boot-starter-freemarker")
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-validation')

    // Cache
    implementation('com.github.ben-manes.caffeine:caffeine')

    // DB
    implementation('org.flywaydb:flyway-core')
    implementation("org.xerial:sqlite-jdbc:3.36.0.3")
    implementation("com.github.gwenn:sqlite-dialect:0.1.2")


    // Utils
    implementation('commons-net:commons-net:3.6')
    implementation('org.apache.commons:commons-lang3:3.7')
    implementation('com.github.oshi:oshi-core:3.5.0')

    // DevTools
    runtimeOnly('org.springframework.boot:spring-boot-devtools')
    kapt('org.springframework.boot:spring-boot-configuration-processor')

    //Cryptography
    implementation("org.bouncycastle:bcprov-jdk15on:1.59")

    //JAXB
    implementation("javax.xml.bind:jaxb-api:2.3.0")

    // Byte code
    implementation("org.ow2.asm:asm:7.0")
    implementation("org.ow2.asm:asm-commons:7.0")

    // SDK
    implementation("io.openfuture:chain-kotlin-sdk") {
        exclude group: 'org.slf4j'
    }

    // Tests
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('io.projectreactor:reactor-test')
    testImplementation('junit:junit:4.13')
    // Monitoring
    implementation('io.micrometer:micrometer-registry-prometheus')
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.resources.srcDirs += 'src/main/resources/frontend'
}

// Jar
jar {
    manifest {
        attributes("Implementation-Version": version)
    }
}

// Kotlin
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
noArg {
    annotations(
            "javax.persistence.MappedSuperclass",
            "javax.persistence.Entity",
            "io.openfuture.chain.core.annotation.NoArgConstructor"
    )
}
allOpen {
    annotations(
            "javax.persistence.MappedSuperclass",
            "javax.persistence.Entity",
            "io.openfuture.chain.core.annotation.OpenClass"
    )
}

// IDEA
idea {
    module {
        def kaptMain = file('build/generated/source/kapt/main')
        sourceDirs += kaptMain
        generatedSourceDirs += kaptMain
    }
}

// Jacoco
jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
check.dependsOn jacocoTestReport